# 비동기 효과

## 비동기의 JS

js에서 비동기 코드를 다루는 가장 쉬운 방법
 - async & await

```javascript
import { promises as fsPromises } from 'fs'

async function copyFile(file1, file2) {
  let data = await fsPromises.readFile(file1, { encoding: 'utf-8' });
  fsPromises.writeFile(file2, data, { encoding: 'utf-8' });
}

copyFile('file1.txt', 'file2.txt')
.catch(e => {
  console.log('There was a problem with copyFile: ' + e.message);
});
```

콜백이나 동기함수를 사용할 수도 있지만
- 파멸의 피라미드(아도겐-코드)
![hadouken](./img/hadouken.jpg)

- 동기 코드들이 나머지 코드의 실행을 막음

## 비동기의 퓨어스크립트

`Aff` 모나드는 자바스크립트의 `async/await` 문법과 비슷함
[Eff는 동기적 효과, Aff는 비동기적 효과](https://stackoverflow.com/a/37663712), Eff->Aff 로는 lifting이 가능하지만, Aff에서 Eff로는 불가 

```purescript
import Prelude
import Data.Either (Either(..))
import Effect.Aff (Aff, attempt, message)
import Effect.Class.Console (log)
import Node.Encoding (Encoding(..))
import Node.FS.Aff (readTextFile, writeTextFile)
import Node.Path (FilePath)

copyFile :: FilePath -> FilePath -> Aff Unit
copyFile file1 file2 = do
  my_data <- readTextFile UTF8 file1
  writeTextFile UTF8 file2 my_data

main :: Aff Unit
main = do
  result <- attempt $ copyFile "file1.txt" "file2.txt"
  case result of
    Left e -> log $ "There was a problem with copyFile: " <> message e
    _ -> pure unit
```
위의 코드를 Node.FS.Async & Node.FS.Sync로 재작성할 수 있지만, 위에서 언급한 방식의 문제를 공유하므로 권장하지 않음

`Aff` 는 `Effect` 모나드와 비슷하다.
- 둘 다 모나드이고
- do 표기법으로 사용할 수 있다.

예를 들어, readTextFile의 시그니쳐를 보면, Aff로 감싸진 String을 반환하는 것을 볼 수 있다.

```purescript
readTextFile :: Encoding -> FilePath -> Aff String
```

do 표기법의 bind 화살표(<-) 를 이용해서 값을 unwrap할 수 있다.

```purescript
my_data <- readTextFile UTF8 file1
```

Aff의 주목할만한 기능은 attempt이다.
이는 Aff 코드를 실행할 때의 예외나 에러를 `Either` 에 저장한다.

```purescript
attempt :: forall a. Aff a -> Aff (Either Error a)
```

## HTTP 클라이언트

`affjax` 라이브러리는 Aff를 이용해서 비동기 AJAX HTTP 요청을 할 수 있게 한다.
- purescript-affjax-web
- purescript-affjax-node

다음은 주어진 URL에 HTTP GET 요청을 하고 응답 body 혹은 에러 메시지를 반환하는 예제이다.

```purescript
import Prelude
import Affjax.Node as AN
import Affjax.ResponseFormat as ResponseFormat
import Data.Either (Either(..))
import Effect.Aff (Aff)

getUrl :: String -> Aff String
getUrl url = do
  result <- AN.get ResponseFormat.string url
  pure case result of
    Left err -> "GET /api response failed to decode: " <> AN.printError err
    Right response -> response.body
```

## 병렬 연산

앞에서 Aff 모나드와 do 표기법을 이용해서 연속적인 비동기 연산들을 합성하는 것을 보았다.
비동기 연산들을 병렬로 합성할 수 있다면 유용할 것이다.
Aff를 이용해서, 우리의 두 연산을 순차적으로 개시함으로써, 간단하게 병렬 연산을 할 수 있다.

`parallel` 패키지는 Aff같은 모나드를 위한 병렬 실행을 하는 `Parallel` 타입 클래스를 정의한다.
이 책의 앞에서 applicative functor들이 병렬 연산을 합성할 때에 강력한 지 보았을 것이다.

사실, Parallel 인스턴스는 모나드 m 과 applicative functor f의 연관성을 정의한다.

```purescript
class (Monad m, Applicative f) <= Parallel f m | m -> f, f -> m where
  sequential :: forall a. f a -> m a
  parallel :: forall a. m a -> f a
```

이 클래스는 두 함수를 정의한다.
- parallel : 모나드 m의 연산을 받고, applicative functor f의 연산으로 변환한다.
- sequential : 위의 변환을 반대로 하는 것.

aff 라이브러리는 Aff 모나드를 위한 Parallel 인스턴스를 제공한다.
가변 레퍼런스를 이용해서 둘 중 어느것이 호출되었는지 추적함으로써 Aff 액션을 병렬로 합성한다.
두 결과가 다 반환되는 경우 최종 결과를 계산해서 메인 연산으로 전달한다.

<!-- Applicative Functor가 임의의 인자를 가지는 함수 lifting을 지원하기 떄문에 -->